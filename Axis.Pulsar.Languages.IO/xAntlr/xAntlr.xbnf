# A subset of the Antlr gammar.
# 
# Pattern: recognizes the escape sequences: &amp; = &, &sol; = /
# Sensitive-value: recognizes the escape sequences: &amp; = &, &quot; = "
# Insensitive-value: recognizes the escape sequences: &amp; = &, &apos; = '

# Antlr Root
$antlr -> +[?[$line-space $comment].* $production +[?[$line-space $comment].1, $production].*]

# Production
$production -> +[$symbol-name $production-options.? $initial-rule-list $alternate-rule-list.* $rule-end]
$initial-rule-list -> +[$line-indent-space $colon $white-space $rule-list]>2
$alternate-rule-list -> +[$line-indent-space $v-bar $white-space $rule-list]>2
$production-options -> +[$line-indent-space $r-chevron $production-option-set]>2
# when more options are added, this should be a set with a min-recognition-count of 1, containing optional items
$production-option-set -> +[$threshold]
$threshold -> +[$white-space $hyphen 'threshold' $white-space $digits]

# Rule
$rule-list -> +[$rule-item +[$white-space $rule-item].*]
$rule-item -> ?[$eof $terminal +[?[$rule-group $ref] $cardinality.?]]
$rule-group -> +[$l-bracket $white-space.* $rule-list $white-space.* $r-bracket]
$terminal -> ?[$pattern $sensitive-literal $insensitive-literal]
$ref -> $symbol-name
$eof -> "$EOF"
$rule-end -> +[$line-indent-space $semi-colon]

# Pattern
$pattern -> +[$f-slash $pattern-value $f-slash $pattern-flags.? $match-cardinality.?]
$match-cardinality -> +[$dot $digits +[$comma ?[$digits $plus $star]].?]
$pattern-flags -> +[$dot #1[$ignore-case-flag $multi-line-flag $single-line-flag $explicit-capture-flag $ignore-whitespace-flag]]
$ignore-case-flag -> 'i'
$multi-line-flag -> 'm'
$single-line-flag -> 's'
$explicit-capture-flag -> 'n'
$ignore-whitespace-flag -> 'x'

# Case sensitive literal
$sensitive-literal -> +[$d-quote $sensitive-value $d-quote]

# Case insensitive literal
$insensitive-literal -> +[$s-quote $insensitive-value $s-quote]

# Spaces
$line-indent-space -> +[$line-space ?[$space.1, $tab.+]]
$line-space -> +[$white-space.0, $new-line]

# General
$symbol-name -> /^[a-zA-Z][\w\-]*$/
$cardinality -> /^[\?\*\+]$/
$space -> /^ $/
$tab -> /^\t$/
$digits -> /^\d+$/
$white-space -> /^[ \t]+$/
$sensitive-value -> /^(\\"|[^"])+$/
$insensitive-value -> /^(\\'|[^'])+$/
$pattern-value -> /^(////|[^//])+$/
$new-line -> /^(\r\n|\r|\n)$/
$comment -> /^\#[^\n\r]*$/
$colon -> ":"
$semi-colon -> ";"
$v-bar -> "|"
$l-bracket -> "("
$r-bracket -> ")"
$f-slash -> "/"
$s-quote -> "'"
$d-quote -> '"'
$dot -> '.'
$comma -> ','
$plus -> '+'
$star -> '*'
$r-chevron -> '>'
$hyphen -> '-'
