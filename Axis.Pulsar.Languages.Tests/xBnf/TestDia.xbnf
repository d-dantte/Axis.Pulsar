# Superset of json to represent Dia
#
# 
#

# Packet
$packet -> ?[
	+[$block-space.?  EOF]
	+[$block-space.? $dia-value +[$block-space $dia-value].* $block-space.? EOF]]


# Ion Value
$dia-value -> ?[
	$dia-clob $dia-blob
	$dia-record
	$dia-list
	$dia-string
	$dia-timestamp
	$dia-decimal $dia-int
	$dia-bool $dia-symbol]


# Spaces
$block-space -> ?[$char-space $block-comment $line-comment].+
$char-space -> ?[$space $tab $new-line $carriage-return $form-feed $vertical-tab]
$space -> ' '
$tab -> '\t'
$new-line -> '\n'
$carriage-return -> '\r'
$form-feed -> '\f'
$vertical-tab -> '\v'


# Comments
$block-comment -> @BlockCommentString
$line-comment -> +['//' $line-comment-content]
$line-comment-content -> /^[^\r\n]*\z/


# Annotation
$annotation-list -> +[$annotation '::' $block-space.?].+
$annotation -> ?[$quoted-symbol $identifier]


# Bool
# NOTE: single-quoted strings ignore case.
$dia-bool -> +[$annotation-list.? ?[$null-bool $true-bool $false-bool]]
$null-bool -> "null.bool"
$true-bool -> 'true'
$false-bool -> 'false'


# Int
$dia-int -> +[$annotation-list.? ?[$null-int $int-number]]
$int-number -> +[$negative-sign.? $int-notation]
$negative-sign -> '-'
$int-notation -> ?[$binary-int $hex-int $regular-int]
$null-int -> "null.int"
$binary-int -> /^0[bB][01](_?[01])*\z/.3,+
$hex-int -> /^0[xX][0-9A-Fa-f](_?[0-9A-Fa-f])*\z/.3,+
$regular-int -> /^(0|([1-9](_?\d)*))\z/.2,+


# Decimal
$dia-decimal -> +[$annotation-list.? ?[$null-decimal $scientific-decimal $regular-decimal]]
$null-decimal -> "null.decimal"
$regular-decimal -> +[$negative-sign.? $regular-int '.' $dia-digits.?]
$scientific-decimal -> +[
	$negative-sign.? $regular-int
	+['.' $dia-digits.?].?
	'E' $number-sign.? $dia-digits]
$dia-digits -> /^\d(_?\d)*\z/.2,+
$number-sign -> ?['+' '-']


# Timestamp
# NOTE: Investigate using recognition thresholds to stop shorter presisions from greedily recognizing failed subsets of
# longer presisions. e.g, '2007-02-23T20:14:33.Z' should fail, but is greedily recognized as a 'day-precision'
$dia-timestamp -> +[$annotation-list.? ?[
	$null-timestamp $millisecond-precision $second-precision
	$minute-precision $day-precision $month-precision
	$year-precision]]
$null-timestamp -> "null.timestamp"
$millisecond-precision -> +[
	$year '-' $month '-' $day $date-delimiter
	$hour ':' $minute ':' $second '.'
	$millisecond $time-zone-offset]>12
$second-precision -> +[$year '-' $month '-' $day $date-delimiter $hour ':' $minute ':' $second $time-zone-offset]>10
$minute-precision -> +[$year '-' $month '-' $day $date-delimiter $hour ':' $minute $time-zone-offset]>7
$day-precision -> +[$year '-' $month '-' $day $date-delimiter.?]
$month-precision -> +[$year '-' $month $date-delimiter]
$year-precision -> +[$year $date-delimiter]
$time-zone-offset -> ?[$utc-offset $minute-precision-offset]
$minute-precision-offset -> +[?['+' '-'] $hour ':' $minute]
$utc-offset -> "Z"
$date-delimiter -> "T"
$year -> /^\d{4,}\z/.4,+
$month -> /^(01|02|03|04|05|06|07|08|09|10|11|12)\z/.2,+
$day -> /^(01|02|03|04|05|06|07|08|09|10|11|12|13|14|15|16|17|18|19|20|21|22|23|24|25|26|27|28|29|30|31)\z/.2,+
$hour -> /^(00|01|02|03|04|05|06|07|08|09|10|11|12|13|14|15|16|17|18|19|20|21|22|23)\z/.2,+
$minute -> /^[0-5]\d\z/.2,+
$second -> /^[0-5]\d\z/.2,+
$millisecond -> /^\d+\z/


# String
$dia-string -> +[$annotation-list.? ?[$null-string $singleline-string $multiline-string]]
$null-string -> "null.string"
$singleline-string -> @Singleline-DQDString
$multiline-string -> +[$block-space.? $ml-string].+
$ml-string -> @Multiline-DQDString


# Symbols
$dia-symbol -> +[$annotation-list.? ?[$null-symbol $quoted-symbol $identifier]]
$null-symbol -> "null.symbol"
$quoted-symbol -> @Singleline-SQDString
$identifier -> @SymbolIdentifier


# Blob
$dia-blob -> +[$annotation-list.? ?[$null-blob $blob-text-value]]
$null-blob -> "null.blob"
$blob-text-value -> @BlobValue


# Clob
$dia-clob -> +[$annotation-list.? ?[$null-clob $clob-text-value]]
$null-clob -> "null.clob"
$clob-text-value -> @ClobValue


# List
$dia-list -> +[$annotation-list.? ?[$null-list $list-value]]
$null-list -> "null.list"
$list-value -> +[
	'[' +[
		$block-space.?
		$dia-value +[
			$block-space.?
			','
			$block-space.?
			$dia-value
		].*
	].?
	$block-space.? ']'
]


# Record
$dia-record -> +[$annotation-list.? ?[$null-record $record-value]]
$null-record -> "null.record"
$record-value -> +[
	'{' +[
		$block-space.?
		$record-field +[
			$block-space.?
			','
			$block-space.?
			$record-field
		].*
	].?
	$block-space.? '}'
]
$record-field -> +[$field-name $block-space.? ':' $block-space.? $dia-value]
$field-name -> ?[
	$singleline-string
	$dia-symbol]

